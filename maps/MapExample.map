map "http://hcisolutions.ch/ig/ig-hci-vacd/StructureMap/MapExample" = "MapExample"

// FHIR Bundle to HCI Form QuestionnaireResponse mapping
// 2022+ Roeland Luykx, copyright HCI Solutions AG


uses "http://hl7.org/fhir/StructureDefinition/Bundle" alias Bundle as source
uses "http://hl7.org/fhir/StructureDefinition/Composition" alias Composition as source
uses "http://hl7.org/fhir/StructureDefinition/Patient" alias Patient as source
uses "http://hl7.org/fhir/StructureDefinition/Organization" alias Organization as source
uses "http://hl7.org/fhir/StructureDefinition/Practitioner" alias Practitioner as source
uses "http://hl7.org/fhir/StructureDefinition/PractitionerRole" alias PractitionerRole as source
uses "http://hl7.org/fhir/StructureDefinition/BackboneElement" alias BackboneElement as source
uses "http://hl7.org/fhir/StructureDefinition/Condition" alias Condition as source
uses "http://hl7.org/fhir/StructureDefinition/Observation" alias Observation as source
uses "http://hl7.org/fhir/StructureDefinition/Identifier" alias Identifier as source

uses "http://hl7.org/fhir/StructureDefinition/QuestionnaireResponse" alias QuestionnaireResponse as target

imports "http://hcisolutions.ch/ig/ig-hci-vacd/StructureMap/GenericMapping"


group ImmunizationAdministrationToHciCdsQrFsme(source bundle : Bundle, target qr : QuestionnaireResponse ) {
  bundle.id as id -> qr.id = id;
   
  bundle -> qr.questionnaire = 'http://hcisolutions.ch/ig/ig-hci-vacd/Questionnaire/CdsFSMEForm' "questionnaire";
  bundle -> qr.status = 'completed' "status";
  bundle.timestamp as ts -> qr.meta as meta, meta.lastUpdated = ts "timestamp";

  bundle.entry as entry then {
    entry then DoContained(entry, qr) "DoContained";
  } "entry";
  
  bundle -> qr.item as item,
    item.linkId = "form",
    item.text = "Vac Check zur FrÃ¼hsommer-Meningoenzephalitis-Impfung; Triage-Fragebogen zum Vac Check durch Apotheker/innen" then {

    
    bundle -> item.item as patientinfo,
      patientinfo.linkId = "form.patientinfo",
      patientinfo.text = "Patientenassoziierte Angaben" then {

      bundle then DoPatinfoFsme(bundle, patientinfo) "DoPatinfo";
     
    } "patientinfo";

    bundle ->  item.item as vaccines,
      vaccines.linkId = "form.vaccines", 
      vaccines.text= "Angaben zu bereits erhaltenen Impfdosen" then {

      bundle then DoAdministration(bundle, vaccines) "immmunization";   
       
    } "vaccines";
  } "form";
}


group DoPatinfoFsme(source bundle : Bundle, target patientinfo : BackboneElement) {
  //form.patientinfo.questSocialFacts
  bundle -> patientinfo.item as questSocial,
    questSocial.linkId ="form.patientinfo.questSocialFacts",
    questSocial.text =  "Soziale Faktoren" then {

    //form.patientinfo.questSocialLivingRisk
    bundle -> patientinfo.item as questSocialLivingRisk,
      questSocialLivingRisk.linkId ="form.patientinfo.questSocialLivingRisk", 
      questSocialLivingRisk.text =  "Ist der Wohnort oder Aufenthalt innerhalb CH ausser Genf und Tessin? (FSME)",
      questSocialLivingRisk.answer as answSocialLivingRisk then {
   
        //form.patientinfo.questRisks
        bundle -> patientinfo.item as questRisks,
          questRisks.linkId ="form.patientinfo.questRisks",
          questRisks.text =  "Risikofaktoren" then {

          //form.patientinfo.questPregnancy
          bundle -> patientinfo.item as questPregnancy then {
            bundle -> questPregnancy.linkId ="form.patientinfo.questPregnancy", questPregnancy.text =  "Ist die Person schwanger?",
              questPregnancy.answer as answPreg /*, answPreg.value = false*/ then {

              bundle.entry as entry where ($this.resource.ofType(FHIR.Condition)) then {
                entry.resource as resource where ($this.code.coding.code != '77386006') then {
                  resource.meta where ($this.profile = 'http://fhir.ch/ig/ch-vacd/StructureDefinition/ch-vacd-medical-problems') then {
                    resource.code as rcode then MedicalProblemsAddToItemSocial(rcode , questSocial) "value";
                    resource.code as rcode then MedicalProblemsAddToItemRisks(rcode , questRisks) "value";         
                  }"meta";
                } "resource";

                entry.resource as resource where ($this.code.coding.code = '77386006') then {
                  entry -> answPreg.value =  true "valueBoolean";
                } "resource";

                //HCIC#300005 "residence or stay whole CH except Geneva and Ticino"
                entry.resource as resource where ($this.code.coding.code = '300005' and $this.code.coding.system = 'http://hcisolutions.ch/ig/ig-hci-vacd/CodeSystem/hci-vacd-cds-cs') then {
                  entry -> answSocialLivingRisk.value =  true "valueBoolean";
                } "resource";
              } "entry";
            } "answPreg";
          }"questPregnancy";
        }"questRisks";
      } "answSocialLivingRisk";
  } "questSocial";

  bundle.entry as entry1 where ($this.resource.ofType(FHIR.AllergyIntolerance)) then {
    bundle -> patientinfo.item as questAll, 
     questAll.linkId ="form.patientinfo.questAllergies",
     questAll.text =  "Allergien / UAW" then {
      
        entry1.resource as res then {
          res.code as code1 then {
            code1.coding as coding1 -> questAll.answer as answ1, 
              answ1.value = coding1 "coding";
          } "code";   
        } "res";
    } "questAll";
  }"entry";
}